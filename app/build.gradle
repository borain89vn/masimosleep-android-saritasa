apply from: 'version.gradle'
apply from: 'artifacts.gradle'
apply from: 'keystore/check.gradle'
apply from: '../config.gradle'
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'
apply plugin: 'kotlin-kapt'
apply plugin: "androidx.navigation.safeargs.kotlin"
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

android {
    compileSdkVersion versions.compileSdkVersion
    buildToolsVersion versions.buildToolsVersion

    defaultConfig {
        applicationId "com.mymasimo.masimosleep"
        minSdkVersion versions.minSdkVersion
        targetSdkVersion versions.targetSdkVersion

        versionCode MasimoSleepVersionCode
        versionName MasimoSleepVersionName

        buildConfigField "String", "BUILD_NUMBER", "\"$MasimoSleepBuildNumber\""
        buildConfigField "String", "GIT_HASH", "\"$MasimoSleepGitHash\""
        buildConfigField "boolean", "ALLOW_EMULATION", "true"

        resValue "string", "display_version", "V${versionName}-${MasimoSleepBuildNumber}"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        // see 'keystore/check.gradle'

        release {
            storeFile releaseKeystoreFile
            storePassword releaseKeystorePassword
            keyAlias releaseKeyAlias
            keyPassword releaseAliasPassword
        }
        debug {
            storeFile debugKeystoreFile
            storePassword debugKeystorePassword
            keyAlias debugKeyAlias
            keyPassword debugAliasPassword
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            shrinkResources false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            matchingFallbacks = ['release']
            buildConfigField "boolean", "ALLOW_EMULATION", "false"
        }
        debug {
            applicationIdSuffix ".debug"
            minifyEnabled false
            signingConfig signingConfigs.debug
            matchingFallbacks = ['debug']
        }
        enterprise {
            initWith buildTypes.debug
            applicationIdSuffix ".enterprise"
            matchingFallbacks = ['debug']
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    buildFeatures {
        viewBinding true
    }

    applicationVariants.all { variant ->
        renameAPK(variant, defaultConfig)
    }
}

repositories {
    maven { url 'https://jitpack.io' }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation project(':airlib')

    // Masimo Libs
    implementation(libraries.masimoCommon) { transitive = true }
    debugImplementation(libraries.masimoAndroidCommonDebug) { transitive = true }
    releaseImplementation(libraries.masimoAndroidCommon) { transitive = true }
    enterpriseImplementation(libraries.masimoAndroidCommonDebug) { transitive = true }
    debugImplementation(libraries.masimoUiDebug) { transitive = true }
    releaseImplementation(libraries.masimoUi) { transitive = true }
    enterpriseImplementation(libraries.masimoUi) { transitive = true }
    implementation(libraries.masimoLogback) { transitive = true }
    implementation(libraries.masimoSleepScore) { transitive = true }

    //Firebase
    implementation platform(libraries.firebaseBom)
    implementation libraries.firebaseAnalytics
    implementation libraries.firebaseCrashlytics
    implementation libraries.firebaseFirestore

    //Kotlin
    implementation libraries.kotlin
    implementation libraries.ktxCore
    implementation libraries.ktxFragment
    implementation libraries.kotlinCoroutines

    implementation libraries.appCompat
    implementation libraries.circleIndicator
    implementation libraries.constraintLayout
    implementation libraries.dagger
    kapt libraries.daggerCompiler
    kapt libraries.lifecycleCompiler
    implementation libraries.lifecycleRuntime
    implementation libraries.lifecycleViewModel
    implementation libraries.lifecycleProcess
    implementation libraries.material
    implementation libraries.mpAndroidChart
    implementation libraries.navFragmentKtx
    implementation libraries.navUiKtx
    implementation libraries.room
    kapt libraries.roomCompiler
    implementation libraries.roomKtx
    implementation libraries.roomRxJava
    implementation libraries.rxAndroid
    implementation libraries.rxJava
    implementation libraries.rxKotlin
    implementation libraries.rxRelay
    implementation libraries.timber
    implementation libraries.viewPager2
    implementation libraries.viewBindingDelegate
    implementation libraries.ripplePulse

    debugImplementation libraries.flipper
    debugImplementation libraries.soloader

    releaseImplementation libraries.flipperNoOp

    enterpriseImplementation libraries.flipper
    enterpriseImplementation libraries.soloader
}